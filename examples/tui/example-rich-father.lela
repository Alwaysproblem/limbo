// Are Frank or Fred rich?

Sort HUMAN
Sort BOOL

Variable x -> HUMAN
Variable y -> HUMAN
Variable z -> HUMAN

Name Frank -> HUMAN
Name Fred -> HUMAN
Name Sally -> HUMAN
Name T -> BOOL

Function fatherOf/1 -> HUMAN
Function rich/1 -> BOOL

KB: rich(fatherOf(Sally)) = T
KB: fatherOf(Sally) = Frank v fatherOf(Sally) = Fred

// It's explicit knowledge that Frank/Fred is the father, then Frank is rich. Same for Fred.
Query: K<0> (fatherOf(Sally) = Frank -> rich(Frank) = T)

// At split level 1, it is believed that that Frank or Fred are rich.
Query: K<0> (rich(Frank) = T v rich(Fred) = T)
Query: K<1> (rich(Frank) = T v rich(Fred) = T)

// At split level 1, it is considered possible that Frank is the father. Same for Fred.
Query: M<0> (fatherOf(Sally) = Frank)
Query: M<1> (fatherOf(Sally) = Frank)

// At split level 1, it is considered possible that Frank is rich. Same for Fred.
Query: M<0> (rich(Frank) = T)
Query: M<1> (rich(Frank) = T)

// We know Sally's father is rich, but we don't know who it is.
Query: K<1> ex x (fatherOf(Sally) = x ^ rich(x) = T ^ M<1> fatherOf(Sally) /= x)

