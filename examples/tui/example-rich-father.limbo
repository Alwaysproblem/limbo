// Are Frank or Fred rich?

Sort BOOL, HUMAN
Name T                  -> BOOL
Name Frank, Fred, Sally -> HUMAN
Fun  rich/1             -> BOOL
Fun  fatherOf/1         -> HUMAN
Var  x                  -> HUMAN

// All we know is that Sally's father is Frank or Fred and that he's rich.
KB: fatherOf(Sally) = Frank v fatherOf(Sally) = Fred
KB: rich(fatherOf(Sally)) = T

// It's explicit knowledge that Frank/Fred is the father, then Frank is rich.
K<0> (fatherOf(Sally) = Frank -> rich(Frank) = T)

// At belief level 1, it is believed that that Frank or Fred are rich.
K<0> (rich(Frank) = T v rich(Fred) = T)
K<1> (rich(Frank) = T v rich(Fred) = T)

// At belief level 1, it is considered possible that Frank is the father.
M<0> (fatherOf(Sally) = Frank)
M<1> (fatherOf(Sally) = Frank)

// At belief level 1, it is considered possible that Frank is rich.
M<0> (rich(Frank) = T)
M<1> (rich(Frank) = T)

// We know Sally's father is rich, but we don't know who it is.
K<1> ex x (fatherOf(Sally) = x ^ rich(x) = T ^ M<1> fatherOf(Sally) /= x)

