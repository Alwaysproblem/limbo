// Are Sally and Sonny siblings?

Sort HUMAN
Sort BOOL

Variable x -> HUMAN
Variable y -> HUMAN
Variable z -> HUMAN

Name Mary -> HUMAN
Name Mia -> HUMAN
Name Frank -> HUMAN
Name Fred -> HUMAN
Name Sally -> HUMAN
Name Sonny -> HUMAN
Name T -> BOOL

Function fatherOf/1 -> HUMAN
Function motherOf/1 -> HUMAN
Function siblings/2 -> BOOL

// Sally's parents are Frank and either Mary or Mia.
KB: motherOf(Sally) == Mary || motherOf(Sally) == Mia
KB: fatherOf(Sally) == Frank

// Sonny's parents are Mary and either Frank or Fred.
KB: motherOf(Sonny) == Mary
KB: fatherOf(Sonny) == Frank || fatherOf(Sonny) == Fred

// When x and y have they same mother or father, they are siblings.
KB: x != y && motherOf(x) == motherOf(y) -> siblings(x, y) == T
KB: x != y && fatherOf(x) == fatherOf(y) -> siblings(x, y) == T
KB: siblings(x, y) == siblings(y, x)

// Mother and father of any person are not siblings.
KB: siblings(motherOf(z), fatherOf(z)) != T

// Mia and Frank or Mary and Fred are siblings.
KB: siblings(Mia, Frank) == T || siblings(Mary, Fred) == T

// If Mia and Frank are siblings, they cannot have Sally together, so Sally's
// mother must be Mary. Mary is also Sonny's mother, so they're siblings.
// If Mary and Fred are siblings, they cannot have Sonny together, so Sonny's
// father must be Frank. Frank is also Sally's father, so they're siblings.
Query: G Know<0> siblings(Sally, Sonny) == T
Query: G Know<1> siblings(Sally, Sonny) == T
Query: G Know<2> siblings(Sally, Sonny) == T

